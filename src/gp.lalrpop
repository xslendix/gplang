use crate::ast::{Node, Operation};
use std::rc::Rc;

#[LALR]
grammar;

pub GP = StmtList;

StmtList: Rc<Node> = List<Stmt> => Rc::new(Node::Block(<>));
List<T>: Vec<T> =
    <mut v:(<T>)*> <e:T> => { v.push(e); v };

Stmt = {
	Block,
	<Expr> "~~",
	"(_Y_)" <Expr> <Block> => Rc::new(Node::While(<>)),
	"(.Y.)" <Expr> <Block> <(r"U+O+H+" <Block>)?> => Rc::new(Node::IfElse(<>)),
	r"ðŸ˜­+" <Identifier> "(" <IdentifierList> ")" <Block> => Rc::new(Node::FunctionDefinition(<>)),
}

Block: Rc<Node> = {
	"Nya!" <StmtList> "nya~",
}

Expr = Set;
ExprList = Comma<Expr>;

Comma<T>: Vec<T> = {
	<mut v:(<T> ",")*> <e:T?> => match e {
		None => v,
		Some(e) => {
			v.push(e);
			v
		}
	}
}

Set: Rc<Node> = {
	<s:Identifier> ">///<" <e:Expr> => Rc::new(Node::VarSet(s.to_string(), e)),
	MathExpr,
}

BinaryOp<Op, Next>: Rc<Node> = {
	BinaryOp<Op, Next> Op Next => Rc::new(Node::BinaryOp(<>)),
	Next,
}

MathExpr: Rc<Node> = BinaryOp<MathOp, Term>;
MathOp: Operation = {
	":3" => Operation::Add,
	">:3" => Operation::Sub,
}

Term: Rc<Node> = BinaryOp<TermOp, Prefix>;
TermOp: Operation = {
	r"[uU][wW][uU]" => Operation::Mul,
	r"[oO][wW][oO]" => Operation::Div,
	":P" => Operation::Mod,
}

Prefix: Rc<Node> = {
	"-" <Prefix> => Rc::new(Node::UnaryNegate(<>)),
	Postfix,
}

Postfix: Rc<Node> = {
	<Identifier> "(" <ExprList> ")" => Rc::new(Node::FunctionCall(<>)),
	Factor,
};

Factor: Rc<Node> = {
	Integer,
	String,
	Identifier => Rc::new(Node::Identifier(<>)),
	"(" <MathExpr> ")"
}

Integer: Rc<Node> = r"8=*D" => Rc::new(Node::Integer(<>.len() as i64 - 2));
String: Rc<Node> = {
	r#"/\*.*\*/"# => {
		let mut str = <>.to_string();
		str.pop();
		str.pop();
		str.remove(0);
		str.remove(0);
		Rc::new(Node::String(str))
	}
}
Identifier: String = r"[a-zA-Z_][a-zA-Z0-9_]*" => <>.to_string();
IdentifierList: Vec<String> = Comma<Identifier>;

match {
	r"[oO][wW][oO]",
	r"[uU][wW][uU]",
	r"U+O+H+",
} else {
	r"[a-zA-Z_][a-zA-Z0-9_]*",
	_,
}

